/**
* # Purpose
* This module manages Azure Network Interface.
*
* # How to use this module
* Include the module in your .tf files as shown below:
* ```
* module "network_interface" {
*   source = "git::https://username@dev.azure.com/asiandevbank/terraform-modules/_git/terraform-azure-network-interface"
*
*   numberedresource              = var.numberedresource
*   app                           = var.app
*   component                     = var.component
*   env                           = var.env
*   private_ip_address_allocation = var.private_ip_address_allocation
*   region                        = var.region
*   resource_group_name           = var.resource_group_name
*   virtual_network_subnet_id     = var.virtual_network_subnet_id
*
*   tags = var.tags
* }
* ```
*
* ## Notes
* Terraform Documentation can be found here: [azurerm_network_interface](https://www.terraform.io/docs/providers/azurerm/r/network_interface.html)
*
* Docs generated by `terraform-docs markdown . > README.md`, [terraform-docs](https://github.com/terraform-docs/terraform-docs)
*/

resource "azurerm_network_interface" "this" {
  name                          = "nic-${var.app}-${var.env}-${var.region.nickname}${var.numberedresource != "0" ? format("-%03g", var.numberedresource) : ""}"
  location                      = data.azurerm_resource_group.this.location
  resource_group_name           = data.azurerm_resource_group.this.name
  enable_accelerated_networking = var.enable_accelerated_networking
  enable_ip_forwarding          = var.enable_ip_forwarding
  internal_dns_name_label       = var.internal_dns_name_label
  tags                          = var.tags

  dynamic "ip_configuration" {
    for_each = { for ip_configuration in var.ip_configurations : "ipcfg_${ip_configuration.name}" => ip_configuration }

    content {
      name                          = ip_configuration.key
      primary                       = ip_configuration.value.primary
      private_ip_address            = ip_configuration.value.private_ip_address
      private_ip_address_allocation = ip_configuration.value.private_ip_address_allocation
      public_ip_address_id          = ip_configuration.value.public_ip_address_id
      private_ip_address_version    = ip_configuration.value.private_ip_address_version
      subnet_id                     = ip_configuration.value.subnet_id
    }
  }
}